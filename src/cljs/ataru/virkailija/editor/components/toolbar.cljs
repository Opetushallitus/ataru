(ns ataru.virkailija.editor.components.toolbar
  (:require
   [ataru.virkailija.component-data.component :as component]
   [re-frame.core :as c :refer [dispatch]]
   [reagent.core :as r]))

(def ^:private toolbar-elements
  {"Lomakeosio"                component/form-section
   "Tekstikentt채"              component/text-field
   "Tekstialue"                component/text-area
   "Pudotusvalikko"            component/dropdown
   "Painikkeet, yksi valittavissa"  component/radio-button
   "Lista, monta valittavissa" component/multiple-choice
   "Infokentt채"                component/info-element})

(def ^:private followup-toolbar-elements
  (select-keys toolbar-elements
    ["Tekstikentt채" "Tekstialue" "Pudotusvalikko" "Lista, monta valittavissa" "Infokentt채" "Painikkeet, yksi valittavissa"]))

(defn ^:private component-toolbar [path toolbar generator]
  (into [:ul.form__add-component-toolbar--list]
    (for [[component-name generate-fn] toolbar
          :when                        (not (and
                                              (vector? path)
                                              (= :children (second path))
                                              (= "Lomakeosio" component-name)))]
      [:li.form__add-component-toolbar--list-item
       [:a {:on-click (fn [evt]
                        (.preventDefault evt)
                        (generator generate-fn))}
        component-name]])))

(defn add-component [path]
  [:div.editor-form__add-component-toolbar
   [component-toolbar
    path
    toolbar-elements
    (fn [generate-fn]
      (dispatch [:generate-component generate-fn path]))]
   [:div.plus-component
    [:span "+"]]])

(defn followup-add-component [option-path generator]
  [:div.editor-form__add-component-toolbar
   [component-toolbar option-path followup-toolbar-elements generator]
   [:div.plus-component
    [:span "+"]]])
